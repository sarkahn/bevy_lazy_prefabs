int = @{ "0" | "-"?
    ~ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    }
float = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ (!".." ~ "." ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
char = { "'" ~ 
(LETTER | PUNCTUATION | SYMBOL | ASCII_DIGIT)
~ "'" }

vec2 = { "Vec2" ~ "{" ~ fields ~ "}" }
vec3 = { "Vec3" ~ "{" ~ fields ~ "}" }  

string_inner = _{ (!"\"" ~ (LETTER | NUMBER | SYMBOL | PUNCTUATION))* }
inner_silent = @{ string_inner }
string = ${ "\"" ~ string_inner ~ "\"" }
array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]"
}
range = { "(" ~ int ~ ".." ~ int ~ ")" }

id_letter = { LETTER | "_" } 
type_name = @{ id_letter ~ (id_letter | NUMBER )* }
field_name = @{ id_letter ~ (id_letter | NUMBER )* }
prefab_name = @{ id_letter ~ (id_letter | NUMBER )* }
material_type_name = @{ id_letter ~ (id_letter | NUMBER )* }
file_name = @{ type_name ~ "." ~ type_name } 

color = ${ color_id ~ color_value }
color_id = _{ "Color::" }
color_value = { "BLUE" | "RED" | "GREEN" | "YELLOW" | "PINK" } 

value = _{ vec2 | vec3 | float | int | color | char | string | range | array | component }

field = { field_name ~ ":" ~ value }
fields = _{ field ~ ("," ~ field)* ~ ","? }

component = { (type_name ~ component_fields*) }
component_field = _{ (field | component) }
component_fields = _{ "{" ~ 
	component_field ~ ("," ~ component_field)* ~ ","? ~ 
    "}" 
    } 
components = _{ component ~ ("," ~ component)* ~ ","? }

prefab = { type_name? ~ ("{" ~ prefab_fields ~ "}") }
prefab_field = _{ ( load | processor | component ) }
prefab_fields = _{ prefab_field ~ ("," ~ prefab_field)* ~ ","? }

processor = { "processor!(" ~ type_name ~ ("{" ~ fields* ~ "}")? ~ ")" }
 
load = { "load!(" ~ file_name ~ ("{" ~ load_fields ~ "}")? ~ ")" }
load_field = _{ (processor | component) }
load_fields = _{ load_field ~ ("," ~ load_field)* ~ ","? }
 
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
