int = @{ "0" | "-"?
    ~ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    }
float = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ (!".." ~ "." ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
char = { "'" ~ 
(LETTER | PUNCTUATION | SYMBOL | ASCII_DIGIT)
~ "'" }

string_inner = _{ (!"\"" ~ (LETTER | NUMBER | SYMBOL | PUNCTUATION))* }
inner_silent = @{ string_inner }
string = ${ "\"" ~ inner_silent ~ "\"" }
array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]"
}
range = { "(" ~ int ~ ".." ~ int ~ ")" }

id_letter = { LETTER | "_" } 
type_name = @{ id_letter ~ (id_letter | NUMBER )* }
field_name = @{ id_letter ~ (id_letter | NUMBER )* }
prefab_name = @{ id_letter ~ (id_letter | NUMBER )* }

value = _{ float | int | char | string | range | array }

field = { field_name ~ ":" ~ value }
fields = _{ field ~ ("," ~ field)* ~ ","? }

component = { type_name ~ component_fields? }
component_fields = _{ "{" ~ fields ~ "}" } 
components = _{ component ~ ("," ~ component)* ~ ","? }

prefab = _{ prefab_name? ~ "(" ~ components ~ ")" }
 
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
